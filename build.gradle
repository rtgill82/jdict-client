import org.apache.tools.ant.filters.*

apply plugin: 'java'

version  '0.2.2'
archivesBaseName = 'jdict-client'
group 'com.github.rtgill82.libs.jdictclient'
project.ext.set('vendor', 'Robert Gill <rtgill82@gmail.com>')

// Build properties
debugBuild = project.getProperty('debugBuild').toBoolean()
lintUnchecked = project.getProperty('lintUnchecked').toBoolean()
commandClient = project.getProperty('commandClient').toBoolean()

processResources {
  filter ReplaceTokens, tokens: [
    'library.name': project.property('name'),
    'library.version': project.property('version'),
    'library.vendor': project.property('vendor')
  ]
}

dependencies {
  testImplementation group: 'junit', name: 'junit', version: '4.+'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'
}

compileJava {
  options.debug = debugBuild
  if (lintUnchecked)
    options.compilerArgs << '-Xlint:unchecked'
}

jar {
  if (commandClient) {
    manifest { attributes 'Main-Class': project.group + ".Dict" }
  }
}

if (!commandClient) {
  sourceSets.main.java.exclude 'com/github/rtgill82/libs/jdictclient/Dict.java'
}

task tags() {
  try {
    exec {
      executable 'ctags'
      args '--tag-relative=yes', '-R', "-f $rootDir/.git/tags", "$rootDir"
    }
  } catch (all) {
    println("ctags command not avaliable")
  }
}

repositories {
  jcenter()
  mavenCentral()
}
